plugins {
    id "maven-publish"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.12.2"
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 5
def myVersionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
def myVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
    compileSdk 36

    defaultConfig {
        minSdk 27
        targetSdk 36
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.tech7fox.myolink'
}

dependencies {
    implementation 'com.android.support:support-annotations:28.0.0'
}

// https://stackoverflow.com/a/34572606/1251958
tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompiler.classpath.files
    })
}

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn "testDebugUnitTest"
    getClassDirectories().setFrom(fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['com/tech7fox/R*.class',
                       '**/BuildConfig.class'
            ]))
    getSourceDirectories().setFrom(files([
            'src/main/java'
    ]))
    getExecutionData().setFrom(files('build/jacoco/testDebugUnitTest.exec'))
}

coveralls {
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    sourceDirs = ['src/main/java']
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/TECH7Fox/myolink")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        bar(MavenPublication) {
            groupId 'com.tech7fox' // Replace with group ID
            artifactId 'myolink'
            version myVersionName
            artifact("$buildDir/outputs/aar/myolink-release.aar")
        }
    }
}